
Функция СформироватьТабличныйДокументПоТаблицеРазличий(СверочнаяТаблица) Экспорт
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	НомерКолонкиТД = 1;
	Для Каждого Колонка Из СверочнаяТаблица.Колонки Цикл
		НомерКолонкиТД=НомерКолонкиТД+1;
		Область = ТабличныйДокумент.Область(1, НомерКолонкиТД);
		Область.Текст = Колонка.Имя;
	КонецЦикла;
	
	Для Каждого СтрокаСверки Из СверочнаяТаблица Цикл
		НомерСтрокиТД = ТабличныйДокумент.ВысотаТаблицы + 1;
		НомерКолонкиТД = 1;
		Область = ТабличныйДокумент.Область(НомерСтрокиТД, НомерКолонкиТД);
		Если СтрокаСверки.___СВЕРКА___ = 0 Тогда
			Область.Текст = "<совпадает>";
			Область.ЦветТекста = WebЦвета.ЗеленыйЛес;
		ИначеЕсли СтрокаСверки.___СВЕРКА___ < 0 Тогда
			Область.Текст = "<ожидаем>";
			Область.ЦветТекста = WebЦвета.ТемноКрасный;
		Иначе // > 0 
			Область.Текст = "<по факту>";
			Область.ЦветТекста = WebЦвета.Красный;
		КонецЕсли;
		
		Для Каждого Колонка Из СверочнаяТаблица.Колонки Цикл
			НомерКолонкиТД=НомерКолонкиТД+1;
			Область = ТабличныйДокумент.Область(НомерСтрокиТД, НомерКолонкиТД);
			Область.Текст = СтрокаСверки[Колонка.Имя];
		КонецЦикла;
	КонецЦикла;
	
	Для счСтр = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Для счКол = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
			Область = ТабличныйДокумент.Область(счСтр, счКол);
			
			Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
			Область.ГраницаСнизу = Линия;
			Область.ГраницаСправа = Линия;
		КонецЦикла;
	КонецЦикла;
	
	
	Возврат ТабличныйДокумент;
КонецФункции
