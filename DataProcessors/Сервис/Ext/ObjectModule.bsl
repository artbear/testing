
Функция ПрочитатьВременнуюТаблицу(ИмяВременнойТаблицы, МВТ_или_Запрос) Экспорт
	Запрос = Новый Запрос;
	Если ТипЗнч(МВТ_или_Запрос) = Тип("МенеджерВременныхТаблиц") Тогда
		Запрос.МенеджерВременныхТаблиц = МВТ_или_Запрос;
	Иначе
		Запрос.МенеджерВременныхТаблиц = МВТ_или_Запрос.МенеджерВременныхТаблиц;
	КонецЕсли;
		
	Запрос.Текст = "выбрать * из " + ИмяВременнойТаблицы;
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ПолучитьТаблицуЗначенийИзСКД(СКД) Экспорт
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД));
	
	НастройкиКомпоновщика = КомпоновщикНастроек.Настройки;
	
	ДетальныеЗаписи = НастройкиКомпоновщика.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальныеЗаписи.Использование = Истина;
	
	Для каждого НаборДанных из СКД.НаборыДанных Цикл
		Для каждого Поле из НаборДанных.Поля Цикл
			ВыбранноеПоле = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));   
			ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(Поле.ПутьКДанным);
		КонецЦикла;
	КонецЦикла;
	
	Для каждого Поле из СКД.ВычисляемыеПоля Цикл
		ВыбранноеПоле = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));   
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(Поле.ПутьКДанным);
	КонецЦикла;
	
	СКД.ПоляИтога.Очистить();
	
	РасположениеИтогов = НастройкиКомпоновщика.ПараметрыВывода.Элементы[1];
	РасположениеИтогов.Значение = РасположениеИтоговКомпоновкиДанных.Нет; 
	РасположениеИтогов.Использование = Истина;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СКД, НастройкиКомпоновщика,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ПолучитьУправляемуюФормуОбъекта(Объект) Экспорт
	//TODO_Расширить пока сделано только для обработки. Сделаю для остальных объектов при необходимости
	
	ИмяОбработки = Объект.Метаданные().Имя;
	Форма = Обработки[ИмяОбработки].ПолучитьФорму();
	ЗначениеВДанныеФормы(Объект, Форма.Объект);
	
	Возврат Форма;
	
КонецФункции