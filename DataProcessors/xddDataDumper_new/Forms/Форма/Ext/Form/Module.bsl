
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьДеревоМетаданных();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоМетаданных()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Дерево = ОбработкаОбъект.ДеревоМетаданных;
	
	СтрокаРодитель = Дерево.Строки.Добавить();
	СтрокаРодитель.Наименование = "Константы";
	СтрокаРодитель.ИндексКартинки = 4;
	СтрокаРодитель.Пометка = Истина;
	
	СтрокаРодитель = Дерево.Строки.Добавить();
	СтрокаРодитель.Наименование = "Справочники";
	СтрокаРодитель.ИндексКартинки = 0;
	СтрокаРодитель.Пометка = Истина;
	
	СтрокаРодитель = Дерево.Строки.Добавить();
	СтрокаРодитель.Наименование = "Документы";
	СтрокаРодитель.ИндексКартинки = 14;
	СтрокаРодитель.Пометка = Истина;
	
	СтрокаРодитель = Дерево.Строки.Добавить();
	СтрокаРодитель.Наименование = "РегистрыСведений";
	СтрокаРодитель.ИндексКартинки = 8;
	СтрокаРодитель.Пометка = Истина;
	
	СтрокаРодитель = Дерево.Строки.Добавить();
	СтрокаРодитель.Наименование = "РегистрыНакопления";
	СтрокаРодитель.ИндексКартинки = 5;
	СтрокаРодитель.Пометка = Истина;
	
	Для Каждого СтрокаРодитель Из Дерево.Строки Цикл
		Для Каждого Справочник Из Метаданные[СтрокаРодитель.Наименование] Цикл
			Строка = СтрокаРодитель.Строки.Добавить();
			Строка.Наименование = Справочник.Имя;
			Строка.ИндексКартинки = СтрокаРодитель.ИндексКартинки;
			Строка.Пометка = СтрокаРодитель.Пометка;
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетаданныхПометкаПриИзменении(Элемент)
	ИДТекущейСтроки = Элементы.ДеревоМетаданных.ТекущаяСтрока;
	ЭлементКоллекции = Объект.ДеревоМетаданных.НайтиПоИдентификатору(ИДТекущейСтроки);
	Для Каждого ПодчиненнаяСтрока Из ЭлементКоллекции.ПолучитьЭлементы() Цикл
		ПодчиненнаяСтрока.Пометка = ЭлементКоллекции.Пометка;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДеревоМетаданных(Команда)
	Для Каждого Строка Из Объект.ДеревоМетаданных.ПолучитьЭлементы() Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ДеревоМетаданных.Свернуть(ИдентификаторСтроки);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоМетаданных(Команда)
	Для Каждого Строка Из Объект.ДеревоМетаданных.ПолучитьЭлементы() Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ДеревоМетаданных.Развернуть(ИдентификаторСтроки);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьСнятьФлажки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	УстановитьСнятьФлажки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьФлажки(Пометка)
	Для Каждого СтрокаРодитель Из Объект.ДеревоМетаданных.ПолучитьЭлементы() Цикл
		СтрокаРодитель.Пометка = Пометка;
		Для Каждого Строка Из СтрокаРодитель.ПолучитьЭлементы() Цикл
			Строка.Пометка = Пометка;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры


&НаСервере
Функция ВыгрузитьНаСервере()
	СводныйТабличныйДокумент = Новый ТабличныйДокумент;
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Для Каждого СтрокаРодитель Из Объект.ДеревоМетаданных.ПолучитьЭлементы() Цикл
		
		Область = СводныйТабличныйДокумент.Область(СводныйТабличныйДокумент.ВысотаТаблицы+2, 2);
		Область.Текст = "// " + ВРег(СтрокаРодитель.Наименование);
		Область.ЦветТекста = WebЦвета.ЗеленыйЛес;
		Область.Шрифт = Новый Шрифт(,11,Истина);
		СводныйТабличныйДокумент.НачатьГруппуСтрок(СтрокаРодитель.Наименование, Ложь);
		
		
		Для Каждого Строка Из СтрокаРодитель.ПолучитьЭлементы() Цикл
			Если Строка.Пометка = Ложь Тогда
				Продолжить;
			КонецЕсли;
			
			Тип = СтрокаРодитель.Наименование;
			Вид = Строка.Наименование;
			ТипДанных = Вычислить("Метаданные." + Тип + "." + Вид).ПолноеИмя();
			
			Попытка
				ТаблицаСДанными = ПолучитьТаблицуЗначенийПоТипуМетаданных(ТипДанных);
				
				Если ТаблицаСДанными.Количество() = 0 Тогда
					Продолжить;
				ИначеЕсли Тип = "Константы" Тогда
					Если НЕ ЗначениеЗаполнено(ТаблицаСДанными[0][0]) Тогда
						Продолжить;
					ИначеЕсли ТипЗнч(ТаблицаСДанными[0][0]) = Тип("ХранилищеЗначения") Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				ТабличныйДокумент = ОбработкаОбъект.ВыгрузитьТаблицуЗначенийВТабличныйДокумент(ТаблицаСДанными, ТипДанных);
				СводныйТабличныйДокумент.Вывести(ТабличныйДокумент);
			Исключение
				Сообщить("Не удалось выгрузить " + ТипДанных + " по причине: " + ОписаниеОшибки());
			КонецПопытки;
		КонецЦикла;
		
		СводныйТабличныйДокумент.ЗакончитьГруппуСтрок();
	КонецЦикла;
	
	Возврат СводныйТабличныйДокумент;
КонецФункции

Функция ПолучитьТаблицуЗначенийПоТипуМетаданных(ТипДанных)
	
	Если Найти(ТипДанных, "Константа") = 1 Тогда
		ТекстЗапроса = 
		"выбрать "+СтрЗаменить(ТипДанных, "Константа.", "")+"
		|из Константы";
	Иначе
		ТекстЗапроса = 
		"выбрать *
		|из "+ТипДанных;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаСДанными = Запрос.Выполнить().Выгрузить();
	
	НайденнаяКолонка = ТаблицаСДанными.Колонки.Найти("ВерсияДанных");
	Если НЕ НайденнаяКолонка = Неопределено Тогда
		ТаблицаСДанными.Колонки.Удалить(НайденнаяКолонка);
	КонецЕсли;
	
	
	Возврат ТаблицаСДанными;
КонецФункции




&НаКлиенте
Процедура Выгрузить(Команда)
	ТабличныйДокумент = ВыгрузитьНаСервере();
	ТабличныйДокумент.Показать()
КонецПроцедуры

