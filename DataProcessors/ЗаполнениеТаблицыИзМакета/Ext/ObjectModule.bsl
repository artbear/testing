#Область ПрограммныйИнтерфейс

Функция СформироватьТекстЗапросаПоМакету(Макет) Экспорт
	КоличествоКолонок = КоличествоКолонокВМакете(Макет);
	
	ТекстЗапроса = "";
	Для счСтр = 3 по Макет.ВысотаСтраницы Цикл
		Если ПустаяСтрока(Макет.Область(счСтр, 1).Текст) Тогда
			Прервать;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса=ТекстЗапроса+Символы.ПС
							+ " объединить все ";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса+Символы.ПС
						+"выбрать ";
		Для счКол = 1 По КоличествоКолонок Цикл
			ИмяКолонки = Макет.Область(1, счКол).Текст;
			ТипКолонки = Макет.Область(2, счКол).Текст;
			Значение = Макет.Область(счСтр, счКол).Текст;
			
			Если НРег(ТипКолонки) = "строка" Тогда
				Значение = """" +Значение+ """";
			ИначеЕсли ПустаяСтрока(Значение) Тогда
				Значение = "null";
			КонецЕсли;
			
			ТекстЗапроса =	ТекстЗапроса + Символы.ПС
							+ Значение
							+ " как " + ИмяКолонки
							+ ?(счКол = КоличествоКолонок, "", ",");
		КонецЦикла;
		
		Если счСтр = 3 Тогда
			ТекстЗапроса = ТекстЗапроса + Символы.ПС
							+ "поместить втТестовыеДанные";
		КонецЕсли;
		
	КонецЦикла;
	
	Попытка
		КонструкторЗапроса = Новый КонструкторЗапроса;
		КонструкторЗапроса.Текст = ТекстЗапроса;
		ТекстЗапроса = КонструкторЗапроса.Текст;
	Исключение
		Сообщить("Ошибка в запросе: " + ОписаниеОшибки(), СтатусСообщения.Важное);
	КонецПопытки;
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоличествоКолонокВМакете(Макет)
	Для сч = 1 По Макет.ШиринаСтраницы Цикл
		Если ЗначениеЗаполнено(Макет.Область(1,сч).Текст) Тогда
			Продолжить;
		КонецЕсли;
		Возврат сч-1;
		Прервать;
	КонецЦикла;
	
	Возврат 0;
КонецФункции

#КонецОбласти
